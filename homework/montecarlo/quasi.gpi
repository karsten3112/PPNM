set terminal svg background "white"
set output "QuasiMC.svg"
set xlabel "N [Number of points]" font "Times New Roman, 18"
set ylabel "error" font "Times New Roman, 18"
set tics out font "Times New Roman, 15"
set key font "Times New Roman, 15"
set title "Error by quasiMC" font "Times New Roman, 20"
set grid
set fit quiet
set fit logfile '/dev/null'

a = 0.01
b = 0.01

f(x) = a/sqrt(x)
g(x) = b/x


fit f(x) "quasi.data" using 1:2 via a
fit g(x) "quasi.data" using 1:2 via b

 
set style line 1 lw 1.2 lc rgb "black" lt 1 ps 0.60 pt 13 pi 1
 
set style line 2 lw 1.2 lc rgb "green" lt 1
set style line 3 lw 1.2 lc rgb "blue" lt 1

plot[1000:10000][0:0.004] f(x) with lines ls 2 title "fit of: 1/sqrt(N)", \
	"quasi.data" using 1:2 with lines ls 1 notitle, \
	"quasi.data" using 1:2 with points ls 1 title "error estimate", \
	g(x) with lines ls 3 title "fit of: 1/N"
